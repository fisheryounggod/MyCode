
     *___________________________________________________________
     *- 挑战：请求取如下函数的全局极大值(0<x<80): 
        
          twoway function                                     ///
              y = 0.3*sin(0.5*x)+ 0.9*cos(0.2*x) + 0.5*ln(x), ///
                  range(0 80) lw(*2)          
     *___________________________________________________________
        
        * 思路：dy/dx = 0
        *  dy/dx = 0.15*cos(0.5*x) -0.18*sin(0.2*x) + 0.5*(1/x)
       
       
     *- 尚不完全的解答：
       
     *------------------------------------------------
       local   h = 0.06    // 步长
       local   d = 1       // 搜索方向的初始值
       local   x = 69      // x 的初始值
       local   j = 0       // 计数器：记录迭代次数
       local  f1 = 1       // f(x) 一阶偏导数的初始值
       local   e = `h'/30  // 收敛判据  
            
      while (abs(`f1')>`e') & (`x'>0&`x'<80){
        `trace'
        local y0 = 0.3*sin(0.5*`x')+ 0.9*cos(0.2*`x') + 0.5*ln(`x')
        local x  = `x' + `d'*`h' 
        local y1 = 0.3*sin(0.5*`x')+ 0.9*cos(0.2*`x') + 0.5*ln(`x')
        local f1 = 0.15*cos(0.5*`x') - 0.18*sin(0.2*`x') + 0.5*(1/`x')
        local f2 = -0.075*sin(0.5*`x') -0.036*cos(0.2*`x') -0.5*(1/`x'^2)
        local d = sign(-`f1'*`f2')
        dis in g "*" _c
        local j = `j' + 1
      }
        dis "e = " `e'    
        dis "x = " `x'     // x 的解
        dis "y = " `y1'    // y 的极小值
        dis "j = " `j'     // 迭代次数
     *----------------------------------------------------           
     *-图示：
       local x: dis %3.1f `x'
       local y: dis %4.2f `y1'
       twoway function                                        ///
              y = 0.3*sin(0.5*x)+ 0.9*cos(0.2*x) + 0.5*ln(x), ///
              range(0 80) lw(thick) xline(`x') yline(`y1')    ///
              text(`=`y1'+0.1' `=`x'+8' "(`x', `y')") 
              
     *-测试：
       * (0) x=69, h=0.06 可以达到全局收敛；
       * (1) x=70, h=0.06 局部极小值；
       * (2) x=68, h=0.06 无法收敛；
       * (3) x=2 , h=0.05 局部极大值；
              
                
